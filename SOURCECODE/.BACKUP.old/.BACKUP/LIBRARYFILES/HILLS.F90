        !*************************************************************!
        !*      Meta-FES-Extract-1.1                                 *!
        !*                                                           *!
        !*      A PARALLEL IMPLEMENTATION OF METADYNAMICS FREE       *!
        !*      ENERGY SURFACE CONSTRUCTION.                         *!
        !*                                                           *!
        !*      DEVELOPED BY VENKAT KAPIL                            *!
        !*      CONTACT : venkat@iitk.ac.in                          *!
        !*                                                           *!
        !*      COPYRIGHT (C) 2015 VENKAT KAPIL                      *!
        !*      SOME RIGHTS RESERVED.                                *!
        !*                                                           *!
        !*************************************************************!

	!*****************************************************************************************!
	!>>>>>> SUBROUTINES <<<<<<<!
	!*****************************************************************************************!
	
	!CHOOSES SUBROUTINE FOR CALCULATINGMETADYNAMICS BIAS.
	SUBROUTINE CALC_HILLS
	USE GLOBAL_VAR
	IMPLICIT NONE
	!FOR 1D METADYNAMICS.
	IF (NMETACOLVAR .EQ. 1) CALL CALC_HILLS_1D
	RETURN
	END SUBROUTINE
	!*****************************************************************************************!

	!CALCULATES 1-D METADYNAMICS BIAS.
	SUBROUTINE CALC_HILLS_1D
	USE GLOBAL_VAR
	IMPLICIT NONE
	INCLUDE 'mpif.h'
	INTEGER ITER1, ITER2, IBIAS, IPROB, ITERCV, IOSTATUS
	REAL*8 DIFSQ
	INTEGER, DIMENSION(:), ALLOCATABLE :: IBEGIN, IEND
	ALLOCATE (IBEGIN(NMETACOLVAR), IEND(NMETACOLVAR))
	!OPENS THE CV TIME SERIES FILE.
	IF (MPIRANK .EQ. 0) THEN
		OPEN (UNIT = 1, FILE = CVFILE, STATUS = 'OLD')
		READ(1,*) ITERCV, (S(ITER2), ITER2 = 1,NDIM)
	ENDIF
	!INTEGRATES OVER HILLS.
	DO ITER1 = 1,NHILL
	!REWEIGHS THE CV TIME SERIES.
		IF (MPIRANK .EQ. 0) THEN
			DO
				IF (ITERCV .GT. TIME(ITER1)) EXIT
				CALL GETIQ()
				ITER2 = IPROB ()
				CALL GETIS()
				IF ((ITER1 .GE. NHILLMIN) .OR. ITER1 .LE. NHILLMAX ) PROB(ITER2) = PROB(ITER2) + DEXP((TBIAS(IBIAS ()) - C(ITER1 / CDUMP)) / TEMP)
				READ(1,*) ITERCV, (S(ITER2), ITER2=1,NDIM)
			ENDDO
		ENDIF
		CALL MPI_BARRIER (MPI_COMM_WORLD, MPIERROR)
	!FINDS RANGE OF INTEGERATION.
		!#IF DEFINED (_SERIAL) 
			IBEGIN(1) = IMIN(1,ITER1)
			IEND(1) = IMAX(1,ITER1)
		!#ELSE
			IF (MOD (NBIN(1),MPITASKS) .EQ. 0) THEN
				MPIBIN(1) = NBIN(IMETACOLVAR(1)) / MPITASKS
				IBEGIN(1) = MAX (MPIRANK * MPIBIN(1) + 1, IMIN(1,ITER1)) 
				IEND(1)   = MIN ((MPIRANK + 1) * MPIBIN(1), IMAX(1,ITER1))
			ELSE
				MPIBIN(1) = NBIN(IMETACOLVAR(1)) / MPITASKS
				IF (MPIRANK .NE. MPITASKS - 1) THEN
					IBEGIN(1) = MAX (MPIRANK * MPIBIN(1) + 1, IMIN(1,ITER1))
					IEND(1)   = MIN ((MPIRANK + 1) * MPIBIN(1), IMAX(1,ITER1))
				ELSE
					
					IBEGIN(1) = MAX (MPIRANK * MPIBIN(1) + 1, IMIN(1,ITER1))
					IEND(1)   = MIN (NBIN(IMETACOLVAR(1)), IMAX(1,ITER1))
				ENDIF
			ENDIF
		!#ENDIF
	!INTEGRATES OVER GRIDS.
		I(IMETACOLVAR(1)) = IBEGIN(1) - 1
		DO  
	!UPDATES GRID POINT.
			I(IMETACOLVAR(1)) = I(IMETACOLVAR(1)) + 1
	!BAIL OUT.
			IF (I(IMETACOLVAR(1)) .GT. IEND(1)) EXIT
	!UPDATES THE BIAS AND REWEIGHTING FACTOR.
			ITER2= IBIAS ()
			CNUM = CNUM - DEXP (BIAS(ITER2) / TEMP)
			CDEN = CDEN - DEXP (BIAS(ITER2) / TEMP * (WTALPHA-1.0D0))
			BIAS(ITER2) = BIAS(ITER2) + HEIGHT(ITER1) * DEXP (-0.50D0*DIFSQ (ITER1)/WIDTH(ITER1)/WIDTH(ITER1))
			CNUM = CNUM + DEXP (BIAS(ITER2) / TEMP)
			CDEN = CDEN + DEXP (BIAS(ITER2) / TEMP * (WTALPHA-1.0D0))
		ENDDO
	!STORES NUMERATOR AND DENOMENATOR OF REWEIGHTING FACTOR EVERY CDUMP STEPS.            	
        	IF (MOD(ITER1,CDUMP) .EQ. 0) THEN
        		CNUMER(ITER1 / CDUMP + 1) = CNUM
        		CDENOM(ITER1 / CDUMP + 1) = CDEN
        	ENDIF
	!#IF DEFINED(_PARALLEL)
	!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$!
	!PATCHES VARIOUS SUB GRIDS.
	!CALCULATES TOTAL BIAS.
		CALL MPI_BARRIER (MPI_COMM_WORLD, MPIERROR)
		CALL MPI_ALLREDUCE (BIAS	, TBIAS	 , NMETACOLVARGRID		, MPI_DOUBLE_PRECISION, MPI_SUM, MPI_COMM_WORLD, MPIERROR)
		IF (MPIERROR .NE. MPI_SUCCESS) THEN
			CALL PRINT_ERROR ('ERROR IN CALCULATION OF TOTAL BIAS.')
        	        ERRORSTAT = .TRUE.
        	        CALL MPI_ABORT (MPI_COMM_WORLD, MPIBIND, MPIERROR)
        	        RETURN
		ENDIF
	!CALCULATES TOTAL REWEIGHTING FACTOR.
		CALL MPI_ALLREDUCE (CNUMER	, TCNUMER, NHILL / CDUMP + 1 	, MPI_DOUBLE_PRECISION, MPI_SUM, MPI_COMM_WORLD, MPIERROR)
		CALL MPI_ALLREDUCE (CDENOM	, TCDENOM, NHILL / CDUMP + 1	, MPI_DOUBLE_PRECISION, MPI_SUM, MPI_COMM_WORLD, MPIERROR)
		IF(MPIERROR .NE. MPI_SUCCESS) THEN
			CALL PRINT_ERROR ('ERROR IN CALCULATION OF TOTAL REWEIGHTING FACTOR.')
        		ERRORSTAT = .TRUE.
        		CALL MPI_ABORT (MPI_COMM_WORLD, MPIBIND, MPIERROR)
        		RETURN
		ENDIF
		C(1:NHILL / CDUMP + 1) = TEMP * DLOG (TCNUMER(1:NHILL / CDUMP + 1) / TCDENOM(1:NHILL / CDUMP + 1))
	!$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$!
	!#ENDIF
	ENDDO
	CALL MPI_BARRIER (MPI_COMM_WORLD, MPIERROR)
	IF (MPIRANK .EQ. 0) CLOSE(1)
	RETURN
	END SUBROUTINE
	!*****************************************************************************************!

	!CHOOSES SUBORUTINE FOR PRINTING METADYNAMICS FREE ENEGRY SURFACE AS A FUNCTION OF COLLECTIVE VARIABES.
	SUBROUTINE PRINT_HILLS
	USE GLOBAL_VAR
	IMPLICIT NONE
	IF (NMETACOLVAR .EQ. 1) CALL PRINT_HILLS_1D 
	RETURN
	END SUBROUTINE
        !*****************************************************************************************!

	!PRINTS THE 1-D METADYNAMICS FREE ENERGY AS A FUNCTON OF COLLECTIVE VARIABLES.
	SUBROUTINE PRINT_HILLS_1D
	USE GLOBAL_VAR
	IMPLICIT NONE
	INCLUDE 'mpif.h'
	INTEGER ITER1, IBIAS
	REAL*8 SMALL
	!OPENS OUTPUT FILE.
	IF( MPIRANK .EQ. 0) THEN
		SMALL = BIAS(1)
		DO ITER1 = 1,NBIN(IMETACOLVAR(1))
			I(IMETACOLVAR(1)) = ITER1
	!FINDS THE GRID POINT.
			CALL GETS
	!FINDS THE SMALLEST FREE ENERGY VALUE.
			SMALL = MIN (SMALL, -WTALPHA * TBIAS(IBIAS ()))
		ENDDO
		OPEN (UNIT = 1, FILE = OUTPUTFILE1, STATUS = 'OLD')
		DO ITER1 = 1,NBIN(IMETACOLVAR(1))
	!SELECTS INDEX OF POINT ON GRID.
			I(IMETACOLVAR(1)) = ITER1
	!FINDS THE GRID POINT.
			CALL GETS
	!PRINTS THE VALUE OF BIAS.
			WRITE(1,*) S(IMETACOLVAR(1)), -WTALPHA * TBIAS(IBIAS ()) - SMALL
		ENDDO
		CLOSE(1)
	ENDIF
	RETURN
	END SUBROUTINE
        !*****************************************************************************************!
	
	!CHOOSES SUBORUTINE FOR PRINTING METADYNAMICS FREE ENEGRY SURFACE AS A FUNCTION OF COLLECTIVE VARIABES.
	SUBROUTINE PRINT_PROB
	USE GLOBAL_VAR
	IMPLICIT NONE
	IF (NDIM .EQ. 1) CALL PRINT_PROB_1D
	IF (NDIM .EQ. 2) CALL PRINT_PROB_2D 
	RETURN
	END SUBROUTINE
	!*****************************************************************************************!

	!PRINTS THE 1-D METADYNAMICS FREE ENERGY AS A FUNCTON OF COLLECTIVE VARIABLES.
	SUBROUTINE PRINT_PROB_1D
	USE GLOBAL_VAR
	IMPLICIT NONE
	INCLUDE 'mpif.h'
	INTEGER ITER1, IPROB
	REAL*8 SMALL
	!OPENS OUTPUT FILE.
	IF( MPIRANK .EQ. 0) THEN
		SMALL = 0.0D0
		DO ITER1 = 1,NBIN(1)
			I(1) =  ITER1
	!FINDS THE GRID POINT.
			CALL GETQ
	!FINDS THE SMALLEST FREE ENERGY VALUE.
			IF (DLOG (PROB(IPROB ())) + 1 .EQ. DLOG(PROB(IPROB ()))) THEN
				PROB(IPROB ()) = DSQRT (-1.0D0)
				CYCLE
			ENDIF
			SMALL = MIN (SMALL, -TEMP * DLOG (PROB(IPROB ())))
		ENDDO
		OPEN (UNIT = 1, FILE = OUTPUTFILE2, STATUS = 'OLD')
		DO ITER1 = 1,NBIN(1)
	!SELECTS INDEX OF POINT ON GRID.
			I(1) = ITER1
	!FINDS THE GRID POINT.
			CALL GETQ
	!PRINTS THE VALUE OF BIAS.
			WRITE(1,*) S(1), -TEMP * DLOG (PROB(IPROB ())) - SMALL
		ENDDO
		CLOSE(1)
	ENDIF
	RETURN
	END SUBROUTINE
        !*****************************************************************************************!


	!PRINTS THE 1-D METADYNAMICS FREE ENERGY AS A FUNCTON OF COLLECTIVE VARIABLES.
	SUBROUTINE PRINT_PROB_2D
	USE GLOBAL_VAR
	IMPLICIT NONE
	INCLUDE 'mpif.h'
	INTEGER ITER1, ITER2, ITER3, IPROB, ISMOOTHPROB,NSMOOTHGRID
	REAL*8 SMALL, DUMMY1
	REAL*8, DIMENSION(:), ALLOCATABLE :: SMOOTHPROB 
	INTEGER, DIMENSION(:), ALLOCATABLE :: NSMOOTHPROB
	!ALLOCATES SMOOTHENED GRID IF REQUIRED.
	IF (FSMOOTH .NE. 1) THEN 
	!CALCULATES NUMEBR OF GRIDPOINTS IN SMOOTHENED GRID.
		NSMOOTHGRID = 1
		DO ITER1=1,NDIM
			NSMOOTHGRID = NSMOOTHGRID * ((NBIN(ITER1) - 1)/FSMOOTH + 1)
		ENDDO
		ALLOCATE (SMOOTHPROB(NSMOOTHGRID), NSMOOTHPROB(NSMOOTHGRID))
		SMOOTHPROB     = 0.0D0
		NSMOOTHPROB    = 0
	ENDIF
	!OPENS OUTPUT FILE.
	IF( MPIRANK .EQ. 0) THEN
		SMALL = 0.0D0
		IF (FSMOOTH .NE. 1) THEN 
			DO ITER1 = 1,NBIN(1)
				DO ITER2 = 1,NBIN(2)
					I(1) = ITER1
					I(2) = ITER2
	!FINDS THE GRID POINT.
					CALL GETQ
	!FINDS THE SMALLEST FREE ENERGY VALUE.
					IF (DLOG (PROB(IPROB ())) + 1 .EQ. DLOG(PROB(IPROB ()))) THEN
						PROB(IPROB ()) = DSQRT (-1.0D0)
						CYCLE
					ENDIF
	!FINDS THE GRID POINT IN THE REDUCED GRID.
					DUMMY1 = NSMOOTHGRID
					ISMOOTHPROB = (I(1) - 1) / FSMOOTH + 1
					DO ITER3 = NDIM,2, - 1
					        DUMMY1 = DUMMY1 / ((NBIN(ITER3) - 1) / FSMOOTH + 1)
					        ISMOOTHPROB = ISMOOTHPROB + DUMMY1 * ((I(ITER3) - 1) / FSMOOTH)
					ENDDO
					SMOOTHPROB(ISMOOTHPROB) = SMOOTHPROB(ISMOOTHPROB)  -TEMP * DLOG (PROB(IPROB ()))
					NSMOOTHPROB(ISMOOTHPROB) = NSMOOTHPROB(ISMOOTHPROB) + 1
				ENDDO
			ENDDO
	!CALCULATES THE AVGERAGE PROBABILITY AND THE SMALLEST VALUE.
			SMOOTHPROB(1:NSMOOTHGRID) = SMOOTHPROB(1:NSMOOTHGRID) / NSMOOTHPROB(1:NSMOOTHGRID)
			DO ITER1 = 1,NSMOOTHGRID
				IF (DLOG (SMOOTHPROB(ITER1)) .NE. DLOG(SMOOTHPROB(ITER1))) CYCLE
	!UPDATES LEAST FREE ENERGY VALUE.
				SMALL = MIN (SMALL, SMOOTHPROB(ITER1))
			ENDDO
		ENDIF
		OPEN (UNIT = 1, FILE = OUTPUTFILE2, STATUS = 'OLD')
		DO ITER1 = 1,NBIN(1)
			DO ITER2 = 1,NBIN(2)
	!SELECTS INDEX OF POINT ON GRID.
				I(1) = ITER1
				I(2) = ITER2
	!FINDS THE GRID POINT.
				CALL GETQ
	!FINDS THE GRID POINT IN THE REDUCED GRID.
				IF (FSMOOTH .NE. 1) THEN
					DUMMY1 = NSMOOTHGRID
					ISMOOTHPROB = (I(1) - 1) / FSMOOTH + 1
					DO ITER3 = NDIM,2, - 1
					        DUMMY1 = DUMMY1 / ((NBIN(ITER3) - 1) / FSMOOTH + 1)
					        ISMOOTHPROB = ISMOOTHPROB + DUMMY1 * ((I(ITER3) - 1) / FSMOOTH)
					ENDDO
	!PRINTS THE FREE ENERGY.
					WRITE(1,'(3F12.5)') (GRIDMIN(ITER3) + GRIDDIF(ITER3) * FSMOOTH * INT((S(ITER3) - GRIDMIN(ITER3)) / GRIDDIF(ITER3)/FSMOOTH), ITER3 =1 ,NDIM),  SMOOTHPROB(ISMOOTHPROB) - SMALL
				ELSE
					WRITE(1,'(3F12.2)') S(1), S(2), -TEMP * PROB(IPROB ())
				ENDIF
			ENDDO
			WRITE(1,*) 
		ENDDO
		CLOSE(1)
		ENDIF
	RETURN
	END SUBROUTINE
        !*****************************************************************************************!

	!PRINTS REWEIGHTING FACTOR AS A FUNCTION OF METADYNAMICS STEPS.
	SUBROUTINE PRINT_C
	USE GLOBAL_VAR
	IMPLICIT NONE
        INCLUDE 'mpif.h'
	INTEGER ITER1
	OPEN (UNIT = 1, FILE = CFILE, STATUS = 'UNKNOWN')
	
	IF (MPIRANK .EQ. 0) THEN
		DO ITER1 = 1,NHILL / CDUMP
			WRITE(1,*) ITER1, C(ITER1)
		ENDDO
	ENDIF
	CLOSE(1)
	RETURN
	END SUBROUTINE
        !*****************************************************************************************!
	!>>>>>> SUBROUTINES <<<<<<<!
	!*****************************************************************************************!
